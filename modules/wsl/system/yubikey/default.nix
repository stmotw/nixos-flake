# kudos https://lgug2z.com/articles/yubikey-passthrough-on-wsl2-with-full-fido2-support/
{
  config,
  lib,
  pkgs,
  ...
}: let
  cfg = config.mine.system.yubikey;

  usbipd-win-auto-attach = pkgs.fetchurl {
    url = "https://raw.githubusercontent.com/dorssel/usbipd-win/v3.1.0/Usbipd/wsl-scripts/auto-attach.sh";
    hash = "sha256-KJ0tEuY+hDJbBQtJj8nSNk17FHqdpDWTpy9/DLqUFaM=";
  };
in {
  options.mine.system.yubikey = {
    enable = lib.mkEnableOption "Enable yubikey integration for WSL";
    autoAttach = lib.mkOption {
      type = lib.types.listOf lib.types.str;
      default = [];
      example = ["4-1"];
      description = "Auto attach devices with provided Bus IDs.";
    };
  };

  config = lib.mkIf cfg.enable {
    environment.systemPackages = [
      pkgs.linuxPackages.usbip
      pkgs.yubikey-manager
      pkgs.libfido2
    ];

    services.pcscd.enable = true;
    services.udev = {
      enable = true;
      packages = [pkgs.yubikey-personalization];
      extraRules = ''
        SUBSYSTEM=="usb", MODE="0666"
        KERNEL=="hidraw*", SUBSYSTEM=="hidraw", TAG+="uaccess", MODE="0666"
      '';
    };

    systemd = {
      services."usbip-auto-attach@" = {
        description = "Auto attach device having busid %i with usbip";
        after = ["network.target"];

        scriptArgs = "%i";
        path = [pkgs.linuxPackages.usbip];

        script = ''
          busid="$1"
          ip="$(grep nameserver /etc/resolv.conf | cut -d' ' -f2)"

          echo "Starting auto attach for busid $busid on $ip."
          source ${usbipd-win-auto-attach} "$ip" "$busid"
        '';
      };

      targets.multi-user.wants = map (busid: "usbip-auto-attach@${busid}.service") cfg.autoAttach;
    };
  };
}
